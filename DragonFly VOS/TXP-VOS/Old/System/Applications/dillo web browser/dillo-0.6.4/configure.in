dnl Process this file with aclocal, autoconf and automake.

AC_INIT(src/dillo.c)
AM_INIT_AUTOMAKE(dillo, 0.6.4)
AM_CONFIG_HEADER(config.h)


dnl Options

AC_ARG_WITH(jpeg-lib, [  --with-jpeg-lib=DIR     Specify where to find libjpeg], LIBJPEG_LIBDIR=$withval)
AC_ARG_WITH(jpeg-inc, [  --with-jpeg-inc=DIR     Specify where to find libjpeg's headers], LIBJPEG_INCDIR=$withval)

AC_ARG_ENABLE(efence, [  --enable-efence         Try to compile and run with Electric Fence],
                    , enable_fence=no)
AC_ARG_ENABLE(gprof,  [  --enable-gprof          Try to compile and run with profiling enabled],
                    , enable_gprof=no)
AC_ARG_ENABLE(insure, [  --enable-insure         Try to compile and run with Insure++],
                    , enable_insure=no)
AC_ARG_ENABLE(ansi,   [  --enable-ansi           Try to compile and run with ANSI flags],
                    , enable_ansi=no)

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_RANLIB

dnl Check for Gtk+ (it checks glib too)

AM_PATH_GTK(1.2.0, [LIBS="$LIBS $GTK_LIBS" CFLAGS="$GTK_CFLAGS $CFLAGS"],
AC_MSG_WARN(Unable to find Gtk+ with a version >= 1.2.0))

dnl Check for compiler options

if eval "test x$GCC = xyes"; then
  if test "`echo $CFLAGS | grep '\-Wall' 2> /dev/null`" = ""; then
    CFLAGS="$CFLAGS -D_REENTRANT -Wall -Waggregate-return"
  fi

  if eval "test x$enable_ansi = xyes"; then
    if test "`echo $CFLAGS | grep '\-ansi' 2> /dev/null`" = ""; then
      CFLAGS="$CFLAGS -ansi"
    fi

    if test "`echo $CFLAGS | grep '\-pedantic' 2> /dev/null`" = ""; then
      CFLAGS="$CFLAGS -pedantic"
    fi
  fi
fi
 

dnl Test for libjpeg

if test -n "$LIBJPEG_LIBDIR"; then
  LIBS="$LIBS -L$LIBJPEG_LIBDIR"
fi  
AC_CHECK_LIB(jpeg, jpeg_destroy_decompress, jpeg_ok=yes, jpeg_ok=no)
if test "x$jpeg_ok" = "xyes"; then
  if test -n "$LIBJPEG_INCDIR"; then
    CPPFLAGS="$CPPFLAGS -I$LIBJPEG_INCDIR"
  fi
  AC_CHECK_HEADERS(jpeglib.h jconfig.h jerror.h jmorecfg.h,
    jpeg_ok=yes, jpeg_ok=no)
  if test "x$jpeg_ok" = "xyes"; then
    JPEG='jpeg'; LIBJPEG_LIB='-ljpeg'
  fi
fi
if test "x$jpeg_ok" = "xno"; then
  AC_MSG_WARN(*** JPEG support will not be included ***)
fi


AC_CHECK_LIB(pthread, pthread_create)
AC_CHECK_LIB(z, zlibVersion)
AC_CHECK_LIB(png, png_check_sig)


#dnl Test
if test "x$enable_efence" = "xyes" ; then
  LIBS="-lefence $LIBS"
fi
if test "x$enable_gprof" = "xyes" ; then
  CFLAGS="$CFLAGS -pg"
fi

dnl Checks for header files.

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h sys/uio.h)

AC_CHECK_FUNCS(socket)

if test "x$enable_insure" = "xyes" ; then
  CC="insure -Zoi \"compiler $CC\""
  LIBS="$LIBS -lstdc++-2-libc6.1-1-2.9.0"
fi

AC_SUBST(LIBJPEG_LIB)
AC_SUBST(datadir)
AC_SUBST(src doc bin util lib)

AC_OUTPUT(Makefile doc/Makefile src/Makefile src/IO/Makefile)
